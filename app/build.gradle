apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

/**
 * Version Naming Scheme: is.gd/j8L2Wm AND is.gd/IfAfAX
 */
ext.versionRelease = 1
ext.versionBreaking = 0
ext.versionFeature = 0
ext.versionFix = 0

android {
    compileSdkVersion 28
    testOptions.unitTests.includeAndroidResources = true
    defaultConfig {
        applicationId "na.komi.kodesh"
        minSdkVersion 14
        targetSdkVersion 28
        versionCode generateVersionCode()
        versionName generateVersionName()
        testInstrumentationRunner "androidx.test.ext.junit.runners.AndroidJUnit4"
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro', 'coroutines.pro'

            // Configure Kotlin compiler optimisations for releases
            kotlinOptions {
                jvmTarget = '1.8'
                freeCompilerArgs = [
                        '-Xno-param-assertions',
                        '-Xno-call-assertions',
                        '-Xno-receiver-assertions'
                ]
            }
        }
        debug {
            ext.enableCrashlytics = false
        }
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/main.kotlin_module'

        exclude("about.html")
        exclude("META-INF/beans.xml")
        exclude("META-INF/eclipse.inf")
        exclude("about_files/LICENSE_CDDL.txt")
        exclude("about_files")
        exclude("META-INF/MANIFEST.MF")
        exclude("META-INF/beans.xml")
        exclude("about.html")
        // Exclude AndroidX version files
        exclude("META-INF/*.version")
        // Exclude consumer proguard files
        exclude("META-INF/proguard/*")
        // Exclude the Firebase/Fabric/other random properties files
        exclude("/*.properties")
        exclude("fabric/*.properties")
        exclude("META-INF/*.properties")
        // Exclude multidex version file
        exclude("androidsupportmultidexversion.txt")
        exclude("VERSION.txt")
        // Exclude kotlin reflection data
        exclude("**/*.kotlin_metadata")
        exclude("kotlin/**")
        exclude("META-INF/*.kotlin_builtins")
        exclude("META-INF/*.kotlin_module")
        exclude("META-INF/rxkotlin.properties")
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError false
        checkReleaseBuilds false
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
            unitTests.returnDefaultValues = true
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    defaultConfig {
        vectorDrawables.useSupportLibrary = true
    }
}
androidExtensions {
    /** For Coroutines LayoutContainer **/
    experimental = true
}

private Integer generateVersionCode() {
    return ext.versionRelease * 10000000 + ext.versionBreaking * 10000  + ext.versionFeature * 100 + ext.versionFix
}

private String generateVersionName() {
    return "${ext.versionRelease}.${ext.versionBreaking}.${ext.versionFeature}.${ext.versionFix}"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    /** Android Support Library **/
    implementation "com.google.android.material:material:1.1.0-alpha04"
    implementation "androidx.appcompat:appcompat:1.0.2"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.recyclerview:recyclerview:1.1.0-alpha02"
    implementation "androidx.core:core-ktx:1.0.1"
    implementation "androidx.preference:preference:1.1.0-alpha03"
    implementation "androidx.preference:preference-ktx:1.1.0-alpha03"
    implementation "androidx.constraintlayout:constraintlayout:2.0.0-alpha3"

    /** ViewModel-LiveData **/
    def lifecycle_version  = "2.0.0"
    implementation "com.github.hadilq.liveevent:liveevent:1.0.0"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    kapt "androidx.lifecycle:lifecycle-compiler:$lifecycle_version"

    /** Room **/
    def room_version  = "2.1.0-alpha04"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    /** Paging **/
    def paging_version = "2.1.0"
    implementation "androidx.paging:paging-runtime:$paging_version"

    /** Katana **/
    def katana_version = '1.3.0'
    implementation "org.rewedigital.katana:katana-android:$katana_version"

    /** Imaging **/
    implementation "com.github.MikeOrtiz:TouchImageView:2.1.1"

    /** Kotlin Coroutines **/
    def coroutines_version = '1.1.1'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    /** Tests **/
    testImplementation "androidx.room:room-testing:$room_version"
    testImplementation "androidx.paging:paging-common:$paging_version"
    testImplementation "androidx.test:core:1.1.0"
    testImplementation "androidx.test.ext:junit:1.1.0"
    testImplementation 'org.robolectric:robolectric:4.2'
    androidTestImplementation 'androidx.test:rules:1.1.1'
    androidTestImplementation "androidx.test.ext:junit:1.1.0"

    /** Leak Canary **/
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.6.3'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.3'

}
